apply plugin: 'java'
apply plugin: 'com.jfrog.bintray'
apply plugin: 'maven'
apply plugin: 'maven-publish'

targetCompatibility = '1.7'
sourceCompatibility = '1.7'

Properties properties = new Properties()
properties.load(project.rootProject.file('local.properties').newDataInputStream())

def compilerVersion = '0.1.7'
def packetName = 'jstp-compiler'

publishing {
    publications {
        DefaultPublication(MavenPublication) {
            artifact file("../out/artifacts/jstp_compiler/jstp-compiler.jar")
            artifact source: file("../out/artifacts/jstp_compiler_sources/jstp-compiler-sources.jar"), classifier: 'sources'
            groupId 'com.metarhia.jstp'
            artifactId packetName
            version compilerVersion
            pom.withXml {
                asNode().children().last() + {
                    resolveStrategy = Closure.DELEGATE_FIRST
                    dependencies {
                        dependency {
                            groupId "com.squareup"
                            artifactId "javapoet"
                            version "1.8.0"
                        }
                    }
                }
            }
        }
    }
}

bintray {
    user = properties.getProperty('bintray.user')
    key = properties.getProperty('bintray.apikey')

    publications = ['DefaultPublication']

    pkg {
        repo = 'maven'
        name = packetName

        version {
            name = compilerVersion
            desc = 'JSTP compiler to generate package handlers for you'
            released = new Date()
        }
    }
}

dependencies {
    compile fileTree(include: ['*.jar'], dir: 'libs')
    compile project(':jstp-core')
    testCompile group: 'junit', name: 'junit', version: '4.11'
}
